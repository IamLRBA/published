---
layout: post
title:  Xtext 2.12.0 Release Notes
date:   2017-05-25 08:00:00
categories: releasenotes
---

Xtext 2.12.0 brings several bug fixes and a new API for tracing generated code.

## New Tracing API for Generators

The new API in the `org.eclipse.xtext.generator.trace` package allows to generate text files along with trace files. These trace files can be used by generic UI such as the "Open Generated File" action as well as domain-specific features such as debugging. It's very easy to add such tracing information to a code generator, e.g. with [Xtend templates](https://www.eclipse.org/xtend/documentation/203_xtend_expressions.html#templates):

```xtend
class MyDslGenerator extends AbstractGenerator {
	
	@TracedAccessors(MyDslFactory)
	static class MyDslTraceExtensions {}
	
	@Inject extension MyDslTraceExtensions

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val m = resource.contents.head as Model
		
		fsa.generateTracedFile("foo/Bar.txt", m, '''
			// generated
			«FOR c : m.types»
				«generateClass(c)»
			«ENDFOR»
		''')
	}
	
	@Traced def generateClass(ClassDeclaration it) '''
		class «_name» {
			«FOR m : members»
				«generateMember(m)»
			«ENDFOR»
		}
	'''
	
	@Traced def dispatch generateMember(Operation it) '''
		«_name»(«FOR it : parameter»«_name» : «_type[name.name]»«ENDFOR») : «_type[name.name]»
	'''
	
	@Traced def dispatch generateMember(Property it) '''
		«_name» : «_type[name.name]»
	'''
	
}
```

Here `_name` and `_type` are [extension methods](https://www.eclipse.org/xtend/documentation/202_xtend_classes_members.html#extension-methods) that are generated for the metamodel of the specific language. They generate text and trace it to the respective part of the DSL source. You can find the [full example project on GitHub](https://github.com/TypeFox/tracing_codegen).

## Bug Lists

- [Fixed GitHub issues](https://github.com/search?utf8=%E2%9C%93&q=is%3Aissue+milestone%3ARelease_2.12+is%3Aclosed+repo%3Aeclipse%2Fxtext+repo%3Aeclipse%2Fxtext-core+repo%3Aeclipse%2Fxtext-lib+repo%3Aeclipse%2Fxtext-extras+repo%3Aeclipse%2Fxtext-eclipse+repo%3Aeclipse%2Fxtext-idea+repo%3Aeclipse%2Fxtext-web+repo%3Aeclipse%2Fxtext-maven+repo%3Aeclipse%2Fxtext-xtend&type=Issues&ref=searchresults)

- [Closed Pull Requests](https://github.com/search?utf8=%E2%9C%93&q=is%3Apr+milestone%3ARelease_2.12+is%3Aclosed+repo%3Aeclipse%2Fxtext+repo%3Aeclipse%2Fxtext-core+repo%3Aeclipse%2Fxtext-lib+repo%3Aeclipse%2Fxtext-extras+repo%3Aeclipse%2Fxtext-eclipse+repo%3Aeclipse%2Fxtext-idea+repo%3Aeclipse%2Fxtext-web+repo%3Aeclipse%2Fxtext-maven+repo%3Aeclipse%2Fxtext-xtend&type=Issues&ref=searchresults)
